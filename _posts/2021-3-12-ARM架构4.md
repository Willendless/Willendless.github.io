---
layout: "post"
title: ARM架构（四）上下文切换
category: 体系结构
tags:
    - os
    - arm
---

上下文切换可能由多种情况引起，例如：

+ 进程调度
+ 同步异常
+ 异步异常

在AArch64中，发生异常的控制流如下

1. 处理器执行中断向量表中对应项内的指令：传入中断的类型（kind：同步，irq，fiq，SError）和发生源（source：current EL with SP0/SPx，lower EL using AArch64/32）
2. `context save`
3. 执行中断处理器(`exception handler`)，会根据上述参数以及ESL_ELx的信息multiplex分发执行对应处理例程
4. `context restore`

在执行异常处理器相关代码的过程中会使用到各种通用寄存器，由于通用寄存器是各个异常级别共享的，异常处理器的执行就会影响之前代码执行过程中的处理器状态。所以执行异常处理器之前，需要保存上下文。之后从异常处理器返回时，需要恢复上下文。这个过程即上下文切换。

实际上，上下文切换前后的上下文均会有所不同：

+ 实现进程切换：需要swap out整个进程的上下文。
+ 实现系统调用：修改寄存器的值以提供系统调用返回值。
+ 实现breakpoint异常：修改ELR寄存器，以从下一条指令开始执行，而非当前指令。

下面主要说一下`context save`和`context restore`的具体内容：

## 1. Trap Frame

执行异常处理器前，保存上下文的结构被称为*trap frame*。其实现通常都是将各种状态push进栈中。在push完成以后，栈指针就成为了trap frame的指针。

完整的Cortex-A53执行状态包括了：

+ x0...x30 - all 64-bits of all 31 general purpose registers
+ q0...q31 - all 128-bits of all SIMD/FP registers
+ TPIDR - the 64-bit “thread ID” register
  + 存储在`TPIDR_ELs`
+ sp - the stack pointer
  + 存储在`SP_ELs`
+ PSTATE - the program state
  + 存储在`SPSR_ELx`，也包含了返回的异常级别
+ pc - the program counter
  + `ELR_ELx`存储*prefered link address*，通常`ELR_ELx`为异常发生时的PC的值，或`PC + 4`

如下图，此外，`x31(xzr)`主要是满足ARM对栈指针**16字节**对齐的要求：

![arm-context-switch.png](https://i.loli.net/2021/03/12/lGSUEFc6OwVHP3s.png)

注意：在栈中保存的顺序不一定按该顺序，但是数据均要保存。

## 2. ELx: Preferred Exception Return Address

当异常被带入`ELx`，CPU将*preferred link address*存储在`ELR_ELx`中。该值被定义为：

1. 对于异步异常，第一条没有执行的指令或由于该中断的发生导致没有执行完的指令
2. 对于非系统调用的同步异常，生成该异常的指令的地址
3. 对于异常生成指令（HVC，SVC等），异常生成指令下一条指令的地址

例如，`brk`指令属于第二类。因此，我们需要在异常处理器中手动将该值设置为`ELR_ELx + 4`。


## 3. 示例：上下文切换的实现

注意：每次都保存和恢复SIMD/FP寄存器开销非常昂贵。一种优化是仅在它们被source exception level或异常处理器使用时才进行相关操作。

```c
.global context_save
context_save:
    // FIXME: Save the remaining context to the stack.

    // Save registers begin from x27
    // since we have pushed xzr, lr, x29, x28

    stp x26, x27, [SP, #-16]!
    stp x24, x25, [SP, #-16]!
    stp x22, x23, [SP, #-16]!
    stp x20, x21, [SP, #-16]!
    stp x18, x19, [SP, #-16]!
    stp x16, x17, [SP, #-16]!
    stp x14, x15, [SP, #-16]!
    stp x12, x13, [SP, #-16]!
    stp x10, x11, [SP, #-16]!
    stp x8, x9, [SP, #-16]!
    stp x6, x7, [SP, #-16]!
    stp x4, x5, [SP, #-16]!
    stp x2, x3, [SP, #-16]!
    stp x0, x1, [SP, #-16]!

    stp q30, q31, [SP, #-32]!
    stp q28, q29, [SP, #-32]!
    stp q26, q27, [SP, #-32]!
    stp q24, q25, [SP, #-32]!
    stp q22, q23, [SP, #-32]!
    stp q20, q21, [SP, #-32]!
    stp q18, q19, [SP, #-32]!
    stp q16, q17, [SP, #-32]!
    stp q14, q15, [SP, #-32]!
    stp q12, q13, [SP, #-32]!
    stp q10, q11, [SP, #-32]!
    stp q8, q9, [SP, #-32]!
    stp q6, q7, [SP, #-32]!
    stp q4, q5, [SP, #-32]!
    stp q2, q3, [SP, #-32]!
    stp q0, q1, [SP, #-32]!

    // now free to use general regs to get special regs
    // and push into stack

    mrs x0, SP_EL0
    mrs x1, TPIDR_EL0
    stp x0, x1, [SP, #-16]!
    mrs x0, ELR_EL1
    mrs x1, SPSR_EL1
    stp x0, x1, [SP, #-16]!

    // move current stack pointer to x2 as the third argument
    mov x2, SP

    // save caller regs: lr will be used after function call
    stp lr, xzr, [SP, #-16]!

    // info: x0 <- x29
    mov x0, x29

    // syndrome reg: x1 <- ESR_ELx
    mrs x1, ESR_EL1

    bl handle_exception

    ldp lr, xzr, [SP], #16
    ret

.global context_restore
context_restore:
    // FIXME: Restore the context from the stack.

    // restore by backward order

    ldp x0, x1, [SP], #16
    msr ELR_EL1, x0
    msr SPSR_EL1, x1
    ldp x0, x1, [SP], #16
    msr SP_EL0, x0
    msr TPIDR_EL0, x1

    ldp q0, q1, [SP], #32
    ldp q2, q3, [SP], #32
    ldp q4, q5, [SP], #32
    ldp q6, q7, [SP], #32
    ldp q8, q9, [SP], #32
    ldp q10, q11, [SP], #32
    ldp q12, q13, [SP], #32
    ldp q14, q15, [SP], #32
    ldp q16, q17, [SP], #32
    ldp q18, q19, [SP], #32
    ldp q20, q21, [SP], #32
    ldp q22, q23, [SP], #32
    ldp q24, q25, [SP], #32
    ldp q26, q27, [SP], #32
    ldp q28, q29, [SP], #32
    ldp q30, q31, [SP], #32

    ldp x0, x1, [SP], #16
    ldp x2, x3, [SP], #16
    ldp x4, x5, [SP], #16
    ldp x6, x7, [SP], #16
    ldp x8, x9, [SP], #16
    ldp x10, x11, [SP], #16
    ldp x12, x13, [SP], #16
    ldp x14, x15, [SP], #16
    ldp x16, x17, [SP], #16
    ldp x18, x19, [SP], #16
    ldp x20, x21, [SP], #16
    ldp x22, x23, [SP], #16
    ldp x24, x25, [SP], #16
    ldp x26, x27, [SP], #16

    // x28, x29, x30(lr) and xzr will be handled later
    ret

.macro HANDLER source, kind
    .align 7
    stp     lr, xzr, [SP, #-16]!
    stp     x28, x29, [SP, #-16]!
    
    mov     x29, \source
    movk    x29, \kind, LSL #16
    bl      context_save
    
    bl      context_restore
    ldp     x28, x29, [SP], #16
    ldp     lr, xzr, [SP], #16
    eret
.endm
    
.align 11
.global vectors
vectors:
    // FIXME: Setup the 16 exception vectors.
    HANDLER 0 0
    HANDLER 0 1
    HANDLER 0 2
    HANDLER 0 3
    HANDLER 1 0
    HANDLER 1 1
    HANDLER 1 2
    HANDLER 1 3
    HANDLER 2 0
    HANDLER 2 1
    HANDLER 2 2
    HANDLER 2 3
    HANDLER 3 0
    HANDLER 3 1
    HANDLER 3 2
    HANDLER 3 3
```

### 3.1. trap frame结构

```rust
#[repr(C)]
#[derive(Copy, Clone, Debug)]
pub struct TrapFrame {
    // FIXME: Fill me in.
    pub elr_elx: u64,
    spsr_elx: u64,
    sp_els: u64,
    tpidr_els: u64,
    q: [u128; 32], // q0...q31
    x: [u64; 31], // x0...x30(lr)
    xzr: u64, // for 16byte alignment purpose
}
```

### 3.2. 能处理`brk`异常的异常处理器

```rust
#[no_mangle]
pub extern "C" fn handle_exception(info: Info, esr: u32, tf: &mut TrapFrame) {
    kprintln!("exception happened: {:#?}", info);

    match info.kind {
        Kind::Synchronous => {
            use Syndrome::*;
            
            match Syndrome::from(esr) {
                Brk(k) => {
                    kprintln!("brk exception: {:#?}", k);
                    shell::shell("debug > ");
                    // 这里手动加4，以eret时返回到下一条指令
                    tf.elr_elx += 4;
                },
                other => {
                    kprintln!("sync exception captured: {:#?}", other);
                }
            }
        },
        Kind::Irq => {},
        Kind::Fiq => {},
        Kind::SError => {},
    }
}
```

## 参考

+ [Phase 1: ARM and a Leg](https://tc.gts3.org/cs3210/2020/spring/lab/lab4.html#phase-1-arm-and-a-leg)
+ [ARMv8 Reference Manual](https://tc.gts3.org/cs3210/2020/spring/r/ARMv8-Reference-Manual.pdf)
